plugins {
    id "com.jfrog.bintray" version "1.7"
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

group 'com.transloadit.android.sdk'
version = '0.0.4'
description = "An Android Integration of the Transloadit's(https://transloadit.com) file uploading and encoding service."

android {
    compileSdkVersion 24
    buildToolsVersion "26.0.2"
    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:24.2.1'
    compile 'com.transloadit.sdk:transloadit:0.1.6'
    compile 'io.tus.android.client:tus-android-client:0.1.5'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.+'
    testCompile 'org.mock-server:mockserver-netty:3.9.17'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath = configurations.compile
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'transloadit-android'
                description project.getDescription()
                artifactId 'transloadit-android'
                url 'https://github.com/transloadit/android-sdk'
                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url 'http://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id 'ifedapoolarewaju'
                        name 'Ifedapo Olarewaju'
                        email 'ifedapoolarewaju@gmail.com'
                    }
                }

                scm {
                    url 'https://github.com/transloadit/android-sdk'
                    connection 'https://github.com/transloadit/android-sdk'
                    developerConnection 'https://github.com/transloadit/android-sdk'
                }
            }
        }
    }
}


bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    configurations = ['archives']
    dryRun = true // change to false to publish
    pkg {
        repo = 'maven'
        name = 'transloadit-android'
        userOrg = 'transloadit'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/transloadit/android-sdk.git'
        websiteUrl = 'https://github.com/transloadit/android-sdk'
        issueTrackerUrl = 'https://github.com/transloadit/android-sdk/issues'
        publicDownloadNumbers = true
        version {
            name = project.getVersion()
            desc = project.getDescription()
            vcsTag = project.getVersion()
        }
    }
}
